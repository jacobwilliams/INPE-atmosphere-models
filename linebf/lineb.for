C     LIBRARY LINEB
C
C----
C
C     The library LINEB includes several routines to
C     compute the high atmospheric properties, using the
C     Lineberry version of the Jacchia 1977 model.
C
C----
C
      SUBROUTINE PDYMOS (SA, SU, RJUD, DAFR, GSTI, TE, AD,
     1                   WMOL, RHOD)
C
C------
C
C
C PURPOSE:
C
C     THE SUBROUTINE PDYMOS GIVES THE  TEMPERATURE,  DENSITY
C
C     AND MOLECULAR WEIGHT OF ATMOSPHERE, USING A POLYNOMIAL
C                                                 -
C     APPROXIMATION FOR THE STATIC PROFILE WITH THE  DYNAMIC
C                                                    --
C     MODEL. THE ROUTINE USES ALSO THE SOLAR FLUX DATA FILE.
C     --                               -
C
C INPUTS:
C
C     SA(1) RIGHT ASCENSION OF THE  POINT  IN  QUESTION,  IN
C           RADIANS (0 TO 2.*PI)
C     SA(2) DECLINATION (GEOCENTRIC LATITUDE) OF THE  POINT,
C           IN RADIANS (-PI TO PI).
C     SA(3) GEOCENTRIC ALTITUDE OF THE POINT IN  METERS,  IN
C           THE RANGE 90000. TO 2000000.M.
C     SU(1) RIGHT ASCENSION OF THE SUN AT THE DATE, IN RADI-
C           ANS (O TO 2.*PI).
C     SU(2) SUN DECLINATION IN RADIANS (-PI TO PI)
C     RJUD  MODIFIED JULIAN DATE (IF OUT OF RANGE, THE  SUB-
C           ROUTINE WILL PRINT A MESSAGE AND STOP).
C     DAFR  TIME (UT) OF THE DAY, IN SECONDS.
C     GSTI  GREENWICH SIDERAL TIME, IN RADIANS (0 TO 2.*PI),
C           AT THE TIME DAFR OF THE DATE RJUD.
C
C OUTPUTS:
C
C     TE(1) MEAN EXOSPHERIC TEMPERATURE ABOVE THE  POINT  IN 
C           QUESTION, AS DEFINED BY EQUATION  20,  IN  REFE-
C           RENCE (1), IN KELVIN.
C     TE(2) LOCAL TEMPERATURE AROUND THE POINT, IN KELVIN.
C     AD(1) LOGARITHM IN BASE 10 OF THE HE NUMBER-DENSITY.
C     AD(2) LOGARITHM IN BASE 10 OF THE O2 NUMBER-DENSITY.
C     AD(3) LOGARITHM IN BASE 10 OF THE N2 NUMBER-DENSITY.
C     AD(4) LOGARITHM IN BASE 10 OF THE AR NUMBER-DENSITY.
C     AD(5) LOGARITHM IN BASE 10 OF THE O  NUMBER-DENSITY.
C     AD(6) LOGARITHM IN BASE 10 OF THE H  NUMBER-DENSITY.
C     WMOL  MEAN-MOLECULAR-WEIGHT OF THE ATMOSPHERE  AT  THE
C           POINT IN KG/KGMOL.
C     RHOD  MEAN-MASS-DENSITY OF THE ATMOSPHERE, IN KG/M/M/M
C
C OBS:
C
C     HE    HELIUM
C     O2    MOLECULAR OXYGEN
C     N2    MOLECULAR NITROGEN
C     AR    ARGON
C     O     ATOMIC OXYGEN
C     H     ATOMIC HYDROGEN
C
C SUBCALLS:
C
C     SOFLUD
C     PSDAMO
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C AUTHORS:
C
C     BENTO SILVA DE MATOS   - INPE - S.J.CAMPOS - BR
C     VALDEMIR CARRARA       - INPE - S.J.CAMPOS - BR
C
C DATE:
C
C     MAR 1989              V. 1.0
C
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(3),SU(2),TE(2),AD(6)
      DIMENSION SF(3),SD(15)
C
C------
C
      RLAT    = SA(2)
      RLON    = SA(1) - GSTI
      SIFI    = 0.9792*SIN(RLAT) + 
     1          0.2028*COS(RLAT)*COS(RLON - 5.07891)
      COFI    = SQRT(1 - SIFI*SIFI)
      TAUO    = 2.4 + 4.8*COFI*COFI

      CALL SOFLUD(RJUD,DAFR,SD,OUTR)

      IF(OUTR.NE.0.) THEN
       WRITE(6,*) ' ERROR IN ROUTINE ADYMOS: OUTR = ',
     1            INT(OUTR)
       STOP
      ENDIF

      ND      = (DAFR/3600. - SD(6) + 12. - TAUO)/3.
      SF(1)   = SD(9)
      SF(2)   = SD(10)
      SF(3)   = SD(ND)

      CALL PSDAMO(SA,SU,SF,RJUD,DAFR,GSTI,TE,AD,WMOL,RHOD)

      RETURN
      END

      SUBROUTINE PSMODS (ALTU, RJUD, DAFR, TE,AL, WMOL, RHOD)
C
C------
C
C PURPOSE:
C
C     THE  SUBROUTINE  PSMODS  USES  THE  POLYNOMIAL APPRO-
C                                         -
C     XIMATION FOR THE STATIC MODEL (2) AND THE SOLAR  FLUX
C                      -      ---               -
C     DATA TO OBTAIN THE TEMPERATURE, MOLECULAR WEIGHT  AND
C
C     DENSITY OF LOCAL ATMOSPHERE.
C
C INPUTS:
C
C     ALTU  ALTITUDE  OF  THE  POINT  IN  METERS  (90000  TO
C           2000000).
C     RJUD  MODIFIED  JULIAN  DATE  (IF  OUT  OF  RANGE  THE 
C           ROUTINE WILL PRINT A MESSAGE AND STOP).
C     DAFR  TIME (UT) OF THE DAY, IN SECONDS (0  TO  86400).
C
C OUTPUTS:
C
C     TE(1) MEAN EXOSPHERIC TEMPERATURE ABOVE THE  POINT  IN
C           QUESTION, AS DEFINED BY EQUATION  20,  IN  REFE-
C           RENCE (1), IN KELVIN.
C     TE(2) LOCAL TEMPERATURE AROUND THE POINT, IN KELVIN.
C     AD(1) LOGARITHM IN BASE 10 OF THE HE NUMBER-DENSITY.
C     AD(2) LOGARITHM IN BASE 10 OF THE O2 NUMBER-DENSITY.
C     AD(3) LOGARITHM IN BASE 10 OF THE N2 NUMBER-DENSITY.
C     AD(4) LOGARITHM IN BASE 10 OF THE AR NUMBER-DENSITY.
C     AD(5) LOGARITHM IN BASE 10 OF THE O  NUMBER-DENSITY.
C     AD(6) LOGARITHM IN BASE 10 OF THE H  NUMBER-DENSITY.
C     WMOL  MEAN-MOLECULAR-WEIGHT OF THE ATMOSPHERE  AT  THE 
C           POINT IN KG/KGMOL.
C     RHOD  MEAN-MASS-DENSITY OF THE ATMOSPHERE, IN KG/M/M/M
C
C SUBCALLS:
C
C     SOFLUD
C     PSMADE
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C AUTHORS:
C
C     BENTO SILVA DE MATOS   - INPE - S.J.CAMPOS - BR
C     VALDEMIR CARRARA       - INPE - S.J.CAMPOS - BR
C
C DATE:
C
C     MAR 1989              V. 1.0
C
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION TE(2),AL(6)
      DIMENSION SF(3),SD(15)
C
C------
C
      CALL SOFLUD(RJUD,DAFR,SD,OUTR)

      IF(OUTR.NE.0.) THEN
       WRITE(6,*) ' ERROR IN ROUTINE ASMODS: OUTR = ',
     1            INT(OUTR)
       STOP
      ENDIF

      SF(1)   = SD(9)
      SF(2)   = SD(10)
      VARI    = .154*SD(7)
      SF(3)   = 1.89*DLOG(VARI + DSQRT(VARI*VARI + 1.D0))

      CALL PSMADE(ALTU,SF,TE,AL,WMOL,RHOD)

      RETURN
      END


      SUBROUTINE PSDAMO (SA, SU, SF, RJUD, DAFR, GSTI,
     1                   TE, AD, WMOL, RHOD)
C
C------
C
C PURPOSE:
C
C     THE SUBROUTINE  PSDAMO  GIVES  THE  DENSITY, MOLECULAR
C
C     WEIGHT AND TEMPERATURE OF THE UPPER ATMOSPHERE,  USING
C
C     THE POLYNOMIAL APPROXIMATION (2) FOR THE STATIC MODEL,
C         -                                    -
C     WITH THE DYNAMIC ATMOSPHERIC MODEL OF JACCHIA 1977 (1)
C              -       -           --
C
C INPUTS:
C
C       SA(1)    RIGHT ASCENTION OF THE POINT, IN RADIANS.
C       SA(2)    DECLINATION (GEOCENTRIC  LATITUDE)  OF  THE
C                POINT, IN RADIANS (-PI TO PI).
C       SA(3)    GEOCENTRIC ALTITUDE IN METERS, BETWEEN  THE
C                RANGE 90,000-2,000,000.
C       SU(1)    RIGHT ASCENTION OF THE SUN AT THE DATE,  IN
C                RADIANS (0 TO 2*PI).
C       SU(2)    SUN DECLINATION IN RADIANS (-PI TO PI).
C       SF(1)    DAILY SOLAR FLUX AT  10.7 CM,  ADJUSTED FOR
C                THE DISTANCE BETWEEN THE EARTH AND THE  SUN
C                AT THE DATE, IN 1E-22 W/M/M/HZ.
C       SF(2)    AVERAGED DAILY FLUX  AS DEFINED BY JACCHIA,
C                IN 1E-22 W/M/M/HZ, ADJUSTED FOR THE  EARTH-
C                SUN DISTANCE.
C       SF(3)    3-HOURLY PLANETARY GEOMAGNETIC INDEX KP, AT
C                THE TIME DAFR - TAU, WHERE TAU IS GIVEN  BY
C                JACCHIA'S 1977 MODEL.
C       RJUD     MODIFIED JULIAN  DATE,  REFERED  TO  1950.0
C                (JULIAN DATE-2433282.5).
C       DAFR     TIME (UT) OF THE DAY, IN SECONDS.
C       GSTI     GREENWICH SIDERAL TIME, IN RADIANS, AT  THE
C                TIME DAFR OF THE DATE RJUD (0 TO 2*PI).
C
C OUTPUTS:
C
C       TE(1)    MEAN EXOSPHERIC TEMPERATURE ABOVE THE POINT
C                AS DEFINED BY EQUATION 20 IN THE  JACCHIA'S
C                1977 MODEL, IN KELVIN.
C       TE(2)    LOCAL  TEMPERATURE  AROUND  THE  POINT,  IN
C                KELVIN
C       AD(1)    LOGARITHM BASE 10 OF THE HE NUMBER-DENSITY.
C       AD(2)    LOGARITHM BASE 10 OF THE O2 NUMBER-DENSITY.
C       AD(3)    LOGARITHM BASE 10 OF THE N2 NUMBER-DENSITY.
C       AD(4)    LOGARITHM BASE 10 OF THE AR NUMBER-DENSITY.
C       AD(5)    LOGARITHM BASE 10 OF THE  O NUMBER-DENSITY.
C       AD(6)    LOGARITHM BASE 10 OF THE  H NUMBER-DENSITY.
C       WMOL     MEAN MOLECULAR WEIGHT OF THE ATMOSPHERE  AT
C                THE POINT, IN KG/KGMOL.
C       RHOD     MEAN MASS DENSITY OF THE ATMOSPHERE AT  THE
C                POINT, IN KG/M/M/M.
C OBS:
C       HE       HELIUM
C       O2       MOLECULAR OXYGEN
C       N2       MOLECULAR NITROGEN
C       AR       ARGON
C       O        ATOMIC OXYGEN
C       H        ATOMIC HYDROGEN
C
C SUBCALLS:
C
C       PSMADE
C       DIVARP
C       GEOACP
C       SEALAT
C       SEMIAN
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C                375).
C
C AUTHOR:    VALDEMIR CARRARA       MAR/89            V. 1.0
C            BENTO SILVA DE MATOS
C
      IMPLICIT REAL*8 (A-H,O-Z)
  
      DIMENSION SA(3),SU(2),SF(3),TE(2),AD(6)
      DIMENSION AL(6),WM(6)
  
      COMMON /STADE/ AC(6),TH(6)
C
C------
C  
      DATA AVOG /6.02217D+26/
      DATA WM   /4.0026,31.9988,28.0134,39.948,15.9994,
     1           1.00797/
  
      YEFR   = (RJUD + DAFR/.86400D05)/.3652422D03
      TYFR   = YEFR - IDINT(YEFR)
      GEAC   = SF(3)
      RLAT   = SA(2)
      RLON   = SA(1) - GSTI
      HEIG   = SA(3)
      ALTU   = HEIG/1.D03
      SUDC   = SU(2)
  
      CALL PSMADE(HEIG,SF,TE,AL,WMOL,RHOD)
  
      TEXO   = TE(1)
  
      CALL DIVARP(TEXO,SA,SU,WMOL,AL)
  
      DO I   = 1, 6
       AD(I) = AL(I)
      ENDDO
  
      TQUT   = TH(6)
  
      CALL GEOACP(TQUT,GEAC,RLAT,RLON,ALTU,AL)
  
      DO I   = 1, 6
       AD(I) = AD(I) + AL(I) - AC(I)
      ENDDO
  
      CALL SEALAT(TYFR,SUDC,RLAT,ALTU,AL)
  
      DO I   = 1, 6
       AD(I) = AD(I) + AL(I)
      ENDDO
  
      CALL SEMIAN(TYFR,ALTU,ALCO)
  
      DO I   = 1, 6
       AD(I) = AD(I) + ALCO
      ENDDO
  
      WEIG   = 0.D0
      ANUT   = 0.D0
  
      DO   I = 1, 6
       ANAC  = 10.D0**AD(I)
       ANUT  = ANUT + ANAC
       WEIG  = WEIG + WM(I)*ANAC
      ENDDO
  
      WMOL   = WEIG/ANUT
      RHOD   = WEIG/AVOG
  
      RETURN
      END
  
  
      SUBROUTINE PSMADE (ALTU, SF, TE, AL, WMOL, RHOD)
C
C------
C
C PURPOSE:
C
C     THE SUBROUTINE USES THE POLYNOMIAL  APPROXIMATION  FOR
C                             -
C     THE STATIC   MODEL  (2)  TO  COMPUTE  THE  ATMOSPHERIC
C         -        -                             -
C     DENSITY FOR HEIGHTS FROM 100 TO 2000 KM.
C     --
C
C INPUTS:
C
C       ALTU     ALTITUDE OF THE POINT IN METERS.
C       SF(1)    DAILY OBSERVED  SOLAR  FLUX AT 10.7 CM,  IN
C                1E-22 W/M/M/HZ.
C       SF(2)    AVERAGED DAILY FLUX, AS DEFINED BY JACCHIA,
C                IN 1E-22 W/M/M/HZ.
C
C OUTPUTS:
C
C       TE(1)    MEAN EXOSPHERIC TEMPERATURE ABOVE THE POINT
C                AS DEFINED BY EQUATION 20 IN THE  JACCHIA'S
C                1977 MODEL, IN KELVIN.
C       TE(2)    LOCAL  TEMPERATURE  AROUND  THE  POINT,  IN
C                KELVIN
C       AL(1)    LOGARITHM BASE 10 OF THE HE NUMBER-DENSITY.
C       AL(2)    LOGARITHM BASE 10 OF THE O2 NUMBER-DENSITY.
C       AL(3)    LOGARITHM BASE 10 OF THE N2 NUMBER-DENSITY.
C       AL(4)    LOGARITHM BASE 10 OF THE AR NUMBER-DENSITY.
C       AL(5)    LOGARITHM BASE 10 OF THE  O NUMBER-DENSITY.
C       AL(6)    LOGARITHM BASE 10 OF THE  H NUMBER-DENSITY.
C       WMOL     MEAN MOLECULAR WEIGHT OF THE ATMOSPHERE  AT
C                THE POINT, IN KG/KGMOL.
C       RHOD     MEAN MASS DENSITY OF THE ATMOSPHERE AT  THE
C                POINT, IN KG/M/M/M.
C
C OBS:
C       HE       HELIUM
C       O2       MOLECULAR OXYGEN
C       N2       MOLECULAR NITROGEN
C       AR       ARGON
C       O        ATOMIC OXYGEN
C       H        ATOMIC HYDROGEN
C
C SUBCALLS:
C
C       PMOWEI
C       TEMLO
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C AUTHOR:    VALDEMIR CARRARA        MAR/89            V 1.0
C            BENTO SILVA DE MATOS
C
      IMPLICIT REAL*8 (A-H,O-Z)
  
      DIMENSION SF(3),TE(2),AL(6)
      DIMENSION WM(6),CP(7)
C
C------
C  
      DATA AVOG /6.02217D+26/
      DATA PI   /3.14159265359D0/
  
      DATA WM   /4.0026,31.9988,28.0134,39.948,15.9994,
     1           1.00797/
      DATA TO   /188.D0/
      DATA ZX   /125.D0/
      DATA ZO   /90.0D0/
      DATA BE   /5.5D-5/
  
      FLUX   = SF(1)
      FBAR   = SF(2)
      THAF   = 5.48D0*(FBAR**.8D0) + 101.8D0*(FLUX**.4D0)
      HEIG   = ALTU/1.D3
  
      CALL PMOWEI(THAF,HEIG,AL,WMOL,RHOD)
  
      POTE   = 0.0045D0*(THAF - TO)
      ARGU   = POTE/DSQRT(1.D0 + POTE*POTE)
      TX     = TO + 110.5D0*DATANH(ARGU)
      GX     = 1.9D0*(TX - TO)/(ZX - ZO)
  
      CP(1)  = 2.D0*(TX - TO)/PI
      CP(2)  = GX/CP(1)
      CP(3)  = 1.7D0*CP(2)
      CP(4)  = 2.D0*(THAF - TX)/PI
      CP(5)  = GX/CP(4)
      CP(6)  = BE*CP(5)
      CP(7)  = TX
  
      TE(1)  = THAF
      TE(2)  = TEMLO(HEIG,CP)
  
      RETURN
      END
  
  
      SUBROUTINE DIVARP (TEXO, SA, SU, WMOL, AL)
C
C------
C
C PURPOSE:
C
C     THE SUBROUTINE  CALCULATES THE  DIURNAL VARIATIONS  IN
C                                     --      ---
C     THE ATMOSPHERE DENSITY, AS PROPOSED BY JACCHIA,  USING
C
C     THE POLYNOMIAL APPROXIMATION (2) TO THE STATIC MODEL.
C         -
C
C INPUTS:
C
C       TEXO     MEAN EXOSPHERIC TEMPERATURE, IN KELVIN.
C       SA(1)    RIGHT ASCENTION OF THE POINT, IN RADIANS.
C       SA(2)    DECLINATION (GEOCENTRIC  LATITUDE)  OF  THE
C                POINT, IN RADIANS (-PI TO PI).
C       SA(3)    GEOCENTRIC ALTITUDE IN METERS, BETWEEN  THE
C                RANGE 90,000-2,000,000.
C       SU(1)    RIGHT ASCENTION OF THE SUN AT THE DATE,  IN
C                RADIANS (0 TO 2*PI).
C       SU(2)    SUN DECLINATION IN RADIANS (-PI TO PI).
C       WMOL     MEAN MOLECULAR WEIGHT AT THE LOCAL  REGION,
C                IN KG/KGMOL.
C
C OUTPUTS:
C
C       AL(1)    LOGARITHM BASE 10 OF THE HE NUMBER-DENSITY.
C       AL(2)    LOGARITHM BASE 10 OF THE O2 NUMBER-DENSITY.
C       AL(3)    LOGARITHM BASE 10 OF THE N2 NUMBER-DENSITY.
C       AL(4)    LOGARITHM BASE 10 OF THE AR NUMBER-DENSITY.
C       AL(5)    LOGARITHM BASE 10 OF THE  O NUMBER-DENSITY.
C       AL(6)    LOGARITHM BASE 10 OF THE  H NUMBER-DENSITY.
C       WMOL     MEAN MOLECULAR WEIGHT OF THE ATMOSPHERE  AT
C                THE POINT, IN KG/KGMOL.
C       RHOD     MEAN MASS DENSITY OF THE ATMOSPHERE AT  THE
C                POINT, IN KG/M/M/M.
C
C OBS:
C       HE       HELIUM
C       O2       MOLECULAR OXYGEN
C       N2       MOLECULAR NITROGEN
C       AR       ARGON
C       O        ATOMIC OXYGEN
C       H        ATOMIC HYDROGEN
C
C COMMON OUTPUTS:
C       COMMON /STADE/ AC(6),TH(6)
C                AC     SAME AS  AL,  BUT  USING THE  PSEUDO
C                       EXOSPHERIC   TEMPERATURE   FOR   THE
C                       HYDROGEN.
C                TH(1)  EXOSPHERIC TEMPERATURE FOR HE, IN K.
C                TH(2)  EXOSPHERIC TEMPERATURE FOR O2, IN K.
C                TH(3)  EXOSPHERIC TEMPERATURE FOR N2, IN K.
C                TH(4)  EXOSPHERIC TEMPERATURE FOR AR, IN K.
C                TH(5)  EXOSPHERIC TEMPERATURE FOR  O, IN K.
C                TH(6)  EXOSPHERIC TEMPERATURE FOR  H, IN K.
C
C SUBCALLS:
C
C       PMOWEI
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C AUTHOR:    VALDEMIR CARRARA       MAR/89             V 1.0
C            BENTO SILVA DE MATOS
C
      IMPLICIT REAL*8 (A-H,O-Z)
  
      DIMENSION SA(3),SU(2),AL(6)
      DIMENSION AN(6),WM(6),CP(7)
  
      COMMON /STADE/ AC(6),TH(6)
C
C------
C  
      DATA AVOG /6.02217D+26/
      DATA PI   /3.14159265359D0/
      DATA RAD  /1.7453292778D-2/
  
      DATA WM   /4.0026,31.9988,28.0134,39.948,15.9994,
     1           1.00797/
  
      RLAT   = SA(2)
      DELT   = SU(2)
      SOAN   = SA(1) - SU(1)
      ALTU   = SA(3)/1.D3
      ARGU   = RLAT*RLAT/1.5707963D0
      COAR   = DCOS(ARGU)
      COLA   = DCOS(RLAT)
      E1     = 2.D0 + COAR*COAR
      TEAU   = 1.D0 + 0.3666069D0*DELT*DSIN(RLAT)
  
      DO   I = 1, 5
       BISH  = RAD*(27.D0*(WMOL/WM(I) - 1.D0) - 35.D0)
       ARGU  = SOAN + BISH
       FHAG  = 0.08D0*DCOS(3.D0*ARGU - 1.3089969D0) +
     1         DABS(DCOS(0.5D0*ARGU))**E1
       TH(I) = (0.24D0*COLA*(FHAG - 0.5D0) + TEAU)*TEXO
      ENDDO
  
      ARGU   = SOAN - 1.0471976D0
      FHAG   = 0.08D0*DCOS(3.D0*ARGU - 1.3089969D0) +
     1         DABS(DCOS(0.5D0*ARGU))**E1
      TH(6)  = TEXO*(TEAU + 0.24*COLA*(FHAG - 0.5D0))
  
      DO   I = 1, 6
       TEMP   = TH(I)
       CALL PMOWEI(TEMP,ALTU,AN,WEIG,RHOD)
       AL(I)  = AN(I)
      ENDDO
  
      DO   I = 1, 6
       AC(I)  = AN(I)
      ENDDO
  
      RETURN
      END
  
  
      SUBROUTINE GEOACP (TQUT, GEAC, RLAT, RLON, ALTU, DN)
C
C------
C
C PURPOSE:
C
C     THE  SUBROUTINE  GEOACA  OBTAINS  THE VARIATION OF THE
C
C     ATMOSPHERE  NUMBER-DENSITY,  DUE  TO  THE  GEOMAGNETIC
C                                                ---
C     ACTIVITY, USING A POLYNOMIAL APPROXIMATION (2) FOR THE 
C     --                -
C     STATIC MODEL.
C
C INPUTS:
C
C       TQUT     EXOSPHERIC  TEMPERATURE  FOR  THE  HYDROGEN
C                ABOVE THE POINT, IN KELVIN.
C       GEAC     THE PLANETARY THREE-HOUR INDEX, KP, IN  THE
C                RANGE 0. TO 9.0
C       RLAT     DECLINATION (GEOCENTRIC  LATITUDE)  OF  THE
C                POINT, IN RADIANS (-PI TO PI).
C       RLON     LOCAL LONGITUDE, IN RADIANS (-PI TO PI).
C       ALTU     GEOCENTRIC  ALTITUDE  IN  KM,  IN THE RANGE
C                90. TO 2000..
C
C OUTPUTS:
C
C       DN       ARRAY WITH THE COMMON LOGARITHM  (BASE 10),
C                OF THE VARIATION IN THE  NUMBER DENSITY  OF
C                THE HE (HELIUM), O2 (MOLECULAR OXYGEN),  N2
C                (MOLECULAR NITROGEN), AR (ARGON), O (ATOMIC
C                OXYGEN) AND H (ATOMIC HYDROGEN),  RESPECTI-
C                VELY, DUE TO THE GEOMAGNETIC ACTIVITY.
C
C SUBCALLS:
C
C       PMOWEI
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C AUTHOR:    VALDEMIR CARRARA       APR/87             V 1.0
C            BENTO SILVA DE MATOS
C
      IMPLICIT REAL*8 (A-H,O-Z)
  
      DIMENSION DN(6)
      DIMENSION GM(6)
C
C------
C  
      DATA GM   /-6.30D-05,1.03D-05,0.D0,3.07D-05,-4.85D-05,
     1          0.D0/
  
      AUXI   = 57.5D0*GEAC*(1.D0 + 0.027D0*DEXP(0.4D0*GEAC))
      SILA   = DSIN(RLAT)
      SENO   = 0.9792*SILA +
     1         0.2028*DCOS(RLAT)*DCOS(RLON - 5.0789081D0)
      SEN2   = SENO*SENO
      COL2   = 1.D0 - SEN2
      DELT   = AUXI*SEN2*SEN2
      TEMP   = TQUT + DELT
  
      CALL PMOWEI(TEMP,ALTU,DN,WMOL,RHOD)
  
      DELT   = 0.01D0*DELT
      DEZH   = 5000.D0*DLOG(DSQRT(1.D0 + DELT*DELT) + DELT)
      DELE   = 5.2D-04*AUXI*COL2*COL2
  
      DO   I = 1, 6
       DN(I) = DN(I) + GM(I)*DEZH + DELE
      ENDDO
  
      RETURN
      END
  
      SUBROUTINE PMOWEI (TEXO, ALTU, AN, WMOL, RHOD)
C 
C------
C
C PURPOSE:
C
C     THIS SUBROUTINE USES  A  POLINOMIAL  APPROXIMATION  TO
C                              -
C     COMPUTE THE STATIC DENSITY OF  THE  UPPER  ATMOSPHERE.
C
C     THE POLINOMIAL COEFFICIENTS WERE CALCULATED USING  THE
C
C     JACCHIA'S 1977 MODEL FOR  THE  DENSITY  AND  MOLECULAR
C                    --
C     WEIGHT.
C     ---
C 
C INPUTS:
C
C       TEXO     MEAN EXOSPHERIC TEMPERATURE, IN KELVIN.
C       ALTU     GEOMETRICAL  HEIGHT   WHERE  THE  MOLECULAR
C                WEIGHT WILL BE CALCULATED, IN KM (90-2000).
C
C OUTPUTS:
C
C       AN(1)    LOGARITHM BASE 10 OF THE HE NUMBER-DENSITY.
C       AN(2)    LOGARITHM BASE 10 OF THE O2 NUMBER-DENSITY.
C       AN(3)    LOGARITHM BASE 10 OF THE N2 NUMBER-DENSITY.
C       AN(4)    LOGARITHM BASE 10 OF THE AR NUMBER-DENSITY.
C       AN(5)    LOGARITHM BASE 10 OF THE  O NUMBER-DENSITY.
C       AN(6)    LOGARITHM BASE 10 OF THE  H NUMBER-DENSITY.
C       WMOL     MEAN MOLECULAR WEIGHT OF THE ATMOSPHERE  AT
C                THE POINT, IN KG/KGMOL.
C       RHOD     MEAN MASS DENSITY OF THE ATMOSPHERE AT  THE
C                POINT, IN KG/M/M/M.
C
C OBS:
C       HE       HELIUM
C       O2       MOLECULAR OXYGEN
C       N2       MOLECULAR NITROGEN
C       AR       ARGON
C       O        ATOMIC OXYGEN
C       H        ATOMIC HYDROGEN
C
C SUBCALLS:
C
C       LINEBE
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C AUTHOR:    VALDEMIR CARRARA       MAR/89             V 1.0
C            BENTO SILVA DE MATOS
C
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION AN(6)
      DIMENSION ZL(9),AK(4),WM(6)

      COMMON/LINEA/ AC(6,8,4,4)
      COMMON/LINEB/ BC(6,8,4)
C
C------
C

      DATA ZL /  90., 100., 110., 120., 150., 250., 500.,
     1         1100.,2500. /

      DATA WM /  4.0026, 31.9988, 28.0130, 39.9480, 15.9994,
     1          1.00797 /

      DATA RT   / 6378. /
      DATA AVOG / 6.02217E+26 /

      IF(BC(1,1,1).EQ.0.) CALL LINEBE

      HAGA   = ALTU/(1.+ALTU/RT)
      HAG2   = HAGA*HAGA
      TEX2   = TEXO*TEXO
      WEIG   = 0.
      ANUT   = 0.
      IZ     = 1

      DO WHILE (ALTU.GE.ZL(IZ).AND.IZ.LE.8)
        IZ   = IZ + 1
      ENDDO

      IZ     = IZ - 1
 
      DO   IC = 1 , 6

        DO  IE = 1 , 4
          AK(IE) = AC(IC,IZ,IE,1)      + AC(IC,IZ,IE,2)*TEXO +
     1             AC(IC,IZ,IE,3)/TEXO + AC(IC,IZ,IE,4)/TEX2
        ENDDO
 
        ZLIN     = AK(1) + AK(2)*HAGA + AK(3)/HAGA + AK(4)/HAG2
        AN(IC)   = BC(IC,IZ,1)      + BC(IC,IZ,2)*ZLIN + 
     1             BC(IC,IZ,3)/ZLIN + BC(IC,IZ,4)/ZLIN/ZLIN
        IF(AN(IC).LT.0.) AN(IC)  = 0.

        ANAC     = 10.**AN(IC)
        WEIG     = WEIG + WM(IC) * ANAC
        ANUT     = ANUT + ANAC
      ENDDO

      WMOL = WEIG/ANUT
      RHOD = WEIG/AVOG
 
      RETURN
      END


      SUBROUTINE LINEBE
C 
C------
C
C PURPOSE:
C
C     THIS SUBROUTINE READ  THE  VALUES  OF  THE  POLYNOMIAL
C
C     COEFFICIENTS TO USE IN THE JACCHIA-LINEBERRY MODEL (2)
C                                        ------
C
C COMMON OUTPUTS:
C
C     COMMON/LINEA/ AC(6,8,4,4)
C     COMMON/LINEB/ BC(6,8,4)
C      
C
C REFERENCES:
C
C       [1]      JACCHIA, L. G. "THERMOSPHERIC  TEMPERATURE,
C                DENSITY AND COMPOSITION: NEW MODELS."  CAM-
C                BRIDGE, MA, SAO 1977. (SAO  SPECIAL  REPORT
C                375).
C
C       [2]      MUELLER,  A.  C.  "JACCHIA-LINEBERRY  UPPER
C                ATMOSPHERE DENSITY MODEL".  NASA CR-167824,
C                OCTOBER 1982.
C
C OBS:
C       THIS SUBROUTINE USES A DATA FILE:
C            [DMC.ORBCQ.DAT]LINED.DAT
C       WITCH CONTAINS THE COEFFICIENTS FOR THE MODEL.
C
C AUTHOR:    VALDEMIR CARRARA       MAR/89             V 1.0
C            BENTO SILVA DE MATOS
C
      IMPLICIT REAL*8 (A-H,O-Z)

      DIMENSION AL(4)

      COMMON/LINEA/ AC(6,8,4,4)
      COMMON/LINEB/ BC(6,8,4)
C
C------
C
      OPEN (UNIT=21,FILE='LINED.DAT', ACTION='READ', STATUS='OLD')

      READ (21,*) ((IN,AL,((AC(IJ,IK,IL,IM),IM=1,4),
	1            IL=1,4), IK=1,8), IJ=1,6)

      READ(21,*) ((IN,AL, (BC(IJ,IK,IL),IL=1,4),
     1          IK=1,8),IJ=1,6)

      CLOSE (UNIT=21)

      RETURN
      END
